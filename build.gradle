import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	id 'org.jetbrains.kotlin.jvm' version "$version_kotlin"
    id 'java-library'
    id 'idea'
}

allprojects {
    /* Group name of our artifacts */
    group = 'org.vitrivr'

    /* Repositories for build script. */
    buildscript {
        repositories {
            mavenCentral()
            maven {
                url "https://oss.sonatype.org/content/repositories/snapshots/"
            }
        }
    }

    /* Project repositories for build script. */
    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
}

subprojects {
    /* Common plugins. */
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'idea'

    /* Java Version */
	sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    tasks.withType(KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "17"
        }
    }
	
	kotlin {
        jvmToolchain(17)
    }

    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(17))
        withJavadocJar()
        withSourcesJar()
    }
	
	compileJava {
        options.encoding = 'UTF-8'
	}
	
	/* Common dependencies. */
    dependencies {
        /* Caffeine */
        implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: version_caffeine
    
		/* Kotlin & Kotlinx */
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: version_kotlin
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: version_kotlin
		implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: version_kotlinx_coroutines
    }
}