import org.apache.tools.ant.taskdefs.condition.Os

tasks.register('downloadOpenApiSpec', Exec) {
    workingDir "$buildDir"
    doFirst {
        mkdir("$buildDir/api-client-spec")
    }
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'cmd', '/c', 'curl -o', "$buildDir/api-client-spec/openapi_spec.json", "https://raw.githubusercontent.com/faberf/feature-extraction-server/main/openapi_spec.json"
    } else {
        commandLine 'curl', '-o', "$buildDir/api-client-spec/openapi_spec.json", "https://raw.githubusercontent.com/faberf/feature-extraction-server/main/openapi_spec.json"
    }
}

tasks.register('generateFESClient', org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "kotlin"
    inputSpec = "$buildDir/api-client-spec/openapi_spec.json".toString()
    outputDir = "$buildDir/api-client-generated".toString()
    id = 'vitrivr-engine-module-fes-generate-api-client'
    skipValidateSpec = true
    logToStderr = true
    generateAliasAsModel = false
    enablePostProcessFile = false

    dependsOn 'downloadOpenApiSpec'
}

assemble.dependsOn generateFESClient


sourceSets {
    main {
        kotlin {
            srcDirs += "$buildDir/api-client-generated/src/main/kotlin"
        }
//        resources {
//            srcDirs = ["$rootDir/vitrivr-engine-module-fes/src/main/resources"]
//        }
    }
}



dependencies {
    implementation "com.squareup.moshi:moshi-kotlin:1.14.0"
    implementation "com.squareup.moshi:moshi-adapters:1.14.0"
    implementation "com.squareup.okhttp3:okhttp:4.11.0"
    implementation "com.squareup.okhttp3:logging-interceptor:3.4.1"
    implementation project(path: ':vitrivr-engine-core')
    implementation project(path: ':vitrivr-engine-module-features')
    testImplementation "io.kotlintest:kotlintest-runner-junit5:3.4.2"
}